// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
	Mux16	(a=x, b[0..0]=false, sel=zx, out=xmbzero); //set x to zero if  zx flag is set
	Mux16	(a=y, b[0..0]=false, sel=zy, out=ymbzero); //set y to zero if  zy flag is set
	Not16	(in=xmbzero, out=notx);
	Not16	(in=ymbzero, out=noty);
	Mux16	(a=xmbzero, b=notx, sel=nx, out=xmbnot); //apply bitwise not for x
	Mux16	(a=ymbzero, b=noty, sel=ny, out=ymbnot); //apply bitwise not for y
	Add16	(a=xmbnot, b=ymbnot, out=xplusy);
	And16	(a=xmbnot, b=ymbnot, out=xandy);
	Mux16	(a=xandy, b=xplusy, sel=f, out=almostout);
	Not16	(in=almostout, out=negout);

	// eject multiple output sclices to work with
	Mux16	(a=almostout, b=negout, sel=no, out=out, out[15]=ng, out[0..7]=outfirst, out[8..15]=outsec);

	// split in half and check both whether they are fully zero
	Or8Way	(in=outfirst, out=firsthalfzero);
	Or8Way	(in=outsec, out=secondhalfzero);
	Or		(a=firsthalfzero, b=secondhalfzero, out=notzr);
	Not		(in=notzr, out=zr);
}
